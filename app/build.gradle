apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.akaita.android.easylauncher'

android {
    compileSdkVersion 28
    signingConfigs {
        release {
            keyAlias 'mykeyalias'
            keyPassword 'mykeypassword'
            storeFile file('../keystore.jks')
            storePassword 'mykeystorepassword'
        }
    }
    defaultConfig {
        applicationId "com.tailoredapps.template"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "TAAppTemplate-v$versionName-b$versionCode")
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
    }
    flavorDimensions "backend"
    productFlavors {
        devBackend {
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "TA Template App Dev"
            resValue "string", "leak_canary_display_activity_label", "TA Template App Leaks"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/\""
            dimension "backend"
        }
        productionBackend {
            resValue "string", "app_name", "TA Template App"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/\""
            dimension "backend"
        }
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {

    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}


dependencies {

    /**
     * Local
     */

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':injection')
    implementation project(':core')

    /**
     * UI
     */

    implementation project(':mainscreen')

    implementation "androidx.appcompat:appcompat:$appCompatVersion"

    /**
     * Rx
     */

    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    /**
     * Utils
     */

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

}


easylauncher {
    iconNames "@mipmap/ic_launcher_foreground"
    foregroundIconNames "@mipmap/ic_launcher_foreground"

    defaultFlavorNaming = true

    //For customization see: https://github.com/akaita/easylauncher-gradle-plugin#usage.
    buildTypes {
        debug {
            filters = customColorRibbonFilter("debug", "#ffffff", "#47BAF2")
        }
        release {}
    }
}
