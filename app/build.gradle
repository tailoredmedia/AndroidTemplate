apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.compile_sdk
//    buildToolsVersion "27.0.3"

    signingConfigs {
        release {
            keyAlias 'mykeyalias'
            keyPassword 'mykeypassword'
            storeFile file('../keystore.jks')
            storePassword 'mykeystorepassword'
        }
    }
    defaultConfig {
        applicationId "com.tailoredapps.template"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "TAAppTemplate-v$versionName-b$versionCode")
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
    }
    flavorDimensions "backend"
    productFlavors {
        devBackend {
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "TA Template App Dev"
            resValue "string", "leak_canary_display_activity_label", "TA Template App Leaks"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/\""
            dimension "backend"
        }
        productionBackend {
            resValue "string", "app_name", "TA Template App"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/\""
            dimension "backend"
        }
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {

    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}


dependencies {

    /**
     * Local
     */

    implementation fileTree(dir: 'libs', include: ['*.jar'])


    /**
     * UI
     */
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.material
    implementation deps.androidx.appcompat
    implementation deps.androidx.cardview


    implementation deps.androidx.fragment
    implementation deps.androidx.recyclerview
    kapt deps.androidx.databinding_compiler


    /**
     * Networking
     */

    implementation 'com.google.code.gson:gson:2.8.2'

    implementation deps.retrofit.runtime
    implementation deps.retrofit.gson
    implementation deps.retrofit.rxjava2

    implementation deps.okhttp3.runtime
    implementation deps.okhttp3.okhttp_logging_interceptor


    /**
     * Dependency Injection
     */

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support

    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler


    /**
     * Rx
     */

    implementation deps.rxjava2
    implementation deps.rx_android
    implementation deps.rxkotlin


    /**
     * kotlin
     * */

    implementation deps.kotlin.stdlib


    /**
     * Utils
     */

    implementation deps.timber

    debugImplementation deps.leakcanary
    releaseImplementation deps.leakcanary_no_op

    implementation deps.paperparcel.runtime
    implementation deps.paperparcel.kotlin
    kapt deps.paperparcel.compiler

    /**
     * Testing
     */

    testImplementation deps.junit
    testImplementation deps.mockito.core
    testImplementation "org.powermock:powermock-module-junit4:$powerMockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powerMockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powerMockVersion"
    testImplementation deps.kotlin.stdlib
    testImplementation deps.kotlin.reflect
    testImplementation deps.kotlin.test
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    testImplementation "com.google.code.findbugs:jsr305:3.0.2"

    androidTestImplementation deps.androidx.annotation
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.intents
    androidTestImplementation deps.mockito.android
    androidTestImplementation deps.espresso.contrib, {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation deps.okhttp3.runtime
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation "com.google.code.findbugs:jsr305:3.0.2"

}
